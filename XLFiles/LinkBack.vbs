On Error Resume Next
tb = Chr(9)
nlin = Chr(10) & Chr(9)
nl = Chr(10) & Chr(10)

arg = ""
arg = WScript.Arguments(0)
If arg = "" Then
    MsgBox "This script requires an argument, which is the full path name of a link back file generated by the QuickRDA spreadsheet system.", , "QuickRDA - Link Back Argument Missing"
Else
    t = 1
    While t <> 0
        p = t
        t = InStr(t + 1, arg, "\")
    Wend
    q = InStr(p + 1, arg, "...")
    If q = 0 Then
        Set objFSO = Nothing
        Set objFSO = CreateObject("Scripting.FileSystemObject")
        If objFSO Is Nothing Then
            MsgBox "The Scripting.FileSystemObject cannot be accessed." & nl & "FYI, link back file name that was received as argument to this script is:" & nlin & arg, , "QuickRDA - Access Error"
        Else
            Set objFile = Nothing
            Set objFile = objFSO.OpenTextFile(arg, 1)
            If objFile Is Nothing Then
                MsgBox "The following link back file, which was supplied as argument, either cannot be found or cannot be accessed: " & nlin & arg, , "QuickRDA - Link Back File Not Accessible"
                book = ""
            Else
                book = objFile.ReadLine
                Sheet = objFile.ReadLine
                Row = objFile.ReadLine
            End If
        End If
    Else
        r = InStr(q + 3, arg, "...")
        s = InStr(r + 2, arg, ".xyz")
        book = Mid(arg, p + 1, q - p - 1)
        Sheet = Mid(arg, q + 3, r - q - 3)
        Row = Mid(arg, r + 3, s - r - 3)
    End If
    If book <> "" Then
        Set objExcel = Nothing
        Set objExcel = GetObject(, "Excel.Application.14")
        If objExcel Is Nothing Then
            Set objExcel = GetObject(, "Excel.Application.13")
            If objExcel Is Nothing Then
                Set objExcel = GetObject(, "Excel.Application.12")
                If objExcel Is Nothing Then
                    Set objExcel = GetObject(, "Excel.Application")
                End If
            End If
        End If
        If objExcel Is Nothing Then
            MsgBox "Excel is not running." & nl & "To use this feature, please launch Excel on Workbook:" & nlin & book & nl & "FYI, this link back refers to items defined" & nlin & "in Sheet:" & tb & Sheet & nlin & "on Row:" & tb & Row, , "QuickRDA - Excel not running"
        Else
            objExcel.Visible = True
            Set wkb = Nothing
            Set wkb = objExcel.Workbooks(book)
            If wkb Is Nothing Then
                MsgBox "Excel is running, but apparently does not have open the Workbook: " & nlin & book & nl & "Please open the workbook and try again." & nl & "FYI, this link back refers to items defined" & nlin & "in Sheet:" & tb & Sheet & nlin & "on Row:" & tb & Row, , "QuickRDA - Workbook not found"
            Else
                wkb.Activate
                Set wks = Nothing
                Set wks = wkb.Worksheets(Sheet)
                If wks Is Nothing Then
                    MsgBox "Excel is running, and has the desired open Workbook:" & nlin & book & nl & "but cannot find the desired Sheet: " & nlin & Sheet, , "QuickRDA - Worksheet not found"
                Else
                    wks.Activate
                    wks.Rows(Row).Select
                    'MsgBox "Excel is running and the workbook " & book & " is open but the sheet: " & sheet & " does not have row: " & row & ".",,"QuickRDA - Row not found"
                    
                    Set objWsh = CreateObject("Wscript.Shell")
                    objWsh.AppActivate objExcel.Name
                    'WScript.Shell.AppActivate. objExcel.Name
                End If
            End If
        End If
    End If
End If

